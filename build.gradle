plugins {
    id "com.github.hierynomus.license" version "0.12.1"
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

group 'com.github.kaklakariada'
version = '0.0.1'
sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.compilerArgs << '-Xlint:all'
	options.encoding = 'UTF-8'
}

test {
	if(logger.infoEnabled) {
		testLogging.showStandardStreams = true
	}
	jvmArgs '-XX:+HeapDumpOnOutOfMemoryError', '-enableassertions'
}

dependencies {
	compile 'com.amazonaws:aws-iot-device-sdk-java:1.0.1'
	compile 'com.github.kaklakariada:fritzbox-java-api:0.3.1'
	compile 'org.slf4j:slf4j-api:1.7.13'
	compile 'org.slf4j:jul-to-slf4j:1.7.13'
	
	testRuntime 'ch.qos.logback:logback-classic:1.1.3'
	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:1.10.19'
}

license {
    header = file('gradle/license-header.txt')
    ext.year = 2015
    ext.name = 'Christoph Pirkl'
    ext.email = 'christoph at users.sourceforge.net'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}


bintray {
    def bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    def bintrayApiKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	user = bintrayUser
	key = bintrayApiKey
	publications = ['BintrayPublication']
	pkg {
        repo = 'maven'
        name = project.name
        userOrg = bintrayUser
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/kaklakariada/fritzbox-java-api.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
}
